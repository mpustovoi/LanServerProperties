plugins {
	id 'fabric-loom' version '1.3-SNAPSHOT'
	id 'maven-publish'
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	// modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// javax.annotation.Nullable
	modImplementation 'com.google.code.findbugs:jsr305:3.0.1'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.dev_group

processResources {
	inputs.property "version", project.version
	def props = [
		"version": project.version,
		"minecraft_version_min": project.minecraft_version_min,
		"loader_version": project.loader_version
	]

	filesMatching("fabric.mod.json") {
		expand props
	}
}

// configure  the source folders
sourceSets {
    main {
        java {
            srcDir "../src/main/java"
        }
        resources {
            srcDir "../src/main/resources"
            exclude '.cache'
            exclude 'Thumbs.db'
        }
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"

	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from "LICENSE"
}

// https://github.com/FabricMC/fabric-loom/issues/452
afterEvaluate {
	tasks.remapJar {
		archiveClassifier.set("fabric")
	}
}

import java.io.File;
import java.nio.file.Path;
import java.nio.file.Files;
import net.fabricmc.loom.LoomGradleExtension;
tasks.register("yarn-mc-jar") {
  description = 'Display the path of the yarn-mapped vanilla Minecraft'

  doLast {
    LoomGradleExtension loom = getProject().getExtensions().getByType(LoomGradleExtension.class);
    Path compiledJar = loom.getMappingsProvider().mappedProvider.getMappedJar().toPath();
    println 'The yarn Minecraft jar is located at:'
    println compiledJar

    Dependency dependency = project.getDependencies().create(project.dev_group+":obf2mcp");
	  Set<File> files = project.getConfigurations().detachedConfiguration(dependency).resolve();
	  File file = com.google.common.collect.Iterables.getOnlyElement(files);
	  println file.toPath()
  }
}
